plugins {
  id 'idea'
  id 'org.sonarqube' version '3.3'
}

allprojects {
  group = 'io.svvarm'
  version = '1.0.0-SNAPSHOT'
  repositories.mavenCentral()
}

subprojects {
  apply plugin: 'java-library'
  sourceCompatibility = JavaVersion.VERSION_1_8
  compileJava.options.encoding = 'UTF-8'

  apply plugin: 'jacoco'
  test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
  }

  dependencies {
    // Use Spring Boot BOM to keep dependencies consistent
    implementation platform('org.springframework.boot:spring-boot-dependencies:2.5.5')

    // Use JUnit5 in all projects
    testImplementation 'org.junit.jupiter:junit-jupiter'
  }

  java {
    withJavadocJar()
    withSourcesJar()
  }
}

sonarqube {
  properties {
    property 'sonar.projectKey', 'geojson-jvm'
    property 'sonar.organization', 'svvarm'
    property 'sonar.host.url', 'https://sonarcloud.io'
  }
}
