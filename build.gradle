plugins {
  id 'idea'
  id 'org.sonarqube' version '3.3'
  id 'com.github.spotbugs' version '5.0.6' apply false // only needed by subprojects
}

allprojects {
  group = 'io.svvarm'
  version = '1.0.0-SNAPSHOT'
  repositories.mavenCentral()
}

subprojects {
  apply plugin: 'java-library'
  sourceCompatibility = JavaVersion.VERSION_1_8
  compileJava.options.encoding = 'UTF-8'

  apply plugin: 'jacoco'
  jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports.xml.enabled true // XML report required for SonarQube
  }
  test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
  }

  apply plugin: 'com.github.spotbugs'
  spotbugs.ignoreFailures = true // reported to SonarQube instead of build failures

  apply plugin: 'checkstyle'

  dependencies {
    // Use Spring Boot BOM to keep dependencies consistent
    implementation platform('org.springframework.boot:spring-boot-dependencies:2.7.2')

    // Use JUnit5 and Hamcrest in all projects
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.hamcrest:hamcrest'
  }
}

sonarqube {
  properties {
    property 'sonar.projectKey', 'geojson-jvm'
    property 'sonar.organization', 'svvarm'
    property 'sonar.host.url', 'https://sonarcloud.io'

    property 'sonar.java.spotbugs.reportPaths',
        'build/reports/spotbugs/main.xml,build/reports/spotbugs/test.xml'
    property 'sonar.java.checkstyle.reportPaths',
        'build/reports/checkstyle/main.xml,build/reports/checkstyle/test.xml'
  }
}
